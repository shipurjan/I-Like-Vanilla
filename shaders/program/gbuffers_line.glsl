in_out vec2 lmcoord;
flat in_out vec4 glcolor;
flat in_out vec2 encodedNormal;



#ifdef FSH

void main() {
	vec4 color = glcolor;
	if (color.a < 0.01) discard;
	
	/* DRAWBUFFERS:02 */
	#if DO_COLOR_CODED_GBUFFERS == 1
		color = vec4(0.25, 0.25, 0.25, 1.0);
	#endif
	color.rgb *= 0.5;
	gl_FragData[0] = color;
	gl_FragData[1] = vec4(
		pack_2x8(lmcoord),
		pack_2x8(0.0, 0.3),
		encodedNormal
	);
}

#endif



#ifdef VSH

#include "/utils/projections.glsl"

#if TAA_ENABLED == 1
	#include "/lib/taa_jitter.glsl"
#endif

void main() {
	lmcoord  = (gl_TextureMatrix[1] * gl_MultiTexCoord1).xy;
	adjustLmcoord(lmcoord);
	glcolor = gl_Color;
	encodedNormal = encodeNormal(gbufferModelView[1].xyz);
	
	glcolor.a = 0.5 + 0.5 * glcolor.a;
	
	vec3 viewPos = transform(gl_ModelViewMatrix, vaPosition);
	
	float lineWidth = 0.002;
	gl_Position = viewToNdc(viewPos);
	vec4 offsetPos = viewToNdc(viewPos + vaNormal);
	vec2 screenDir = offsetPos.xy / offsetPos.w - gl_Position.xy / gl_Position.w;
	screenDir = normalize(screenDir) * lineWidth;
	screenDir.xy = screenDir.yx;
	if (glcolor.r + glcolor.b + glcolor.g < 0.1) {
		lmcoord = eyeBrightness / 512.0;
		lmcoord.x = max(lmcoord.x, heldBlockLightValue / 32.0);
		glcolor.a = 1.0 - max(lmcoord.x, lmcoord.y);
	}
	screenDir.x *= -1;
	screenDir *= sign(screenDir.x);
	screenDir.x *= invAspectRatio;
	screenDir *= (gl_VertexID % 2) * 2.0 - 1.0;
	gl_Position.xy += screenDir * gl_Position.w;
	gl_Position.z -= 0.0001;
	
	#if TAA_ENABLED == 1
		doTaaJitter(gl_Position.xy);
	#endif

	
}

#endif
